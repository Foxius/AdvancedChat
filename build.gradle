plugins {
    id 'java'
    id("io.github.goooler.shadow") version "8.1.8"
}

group = 'com.saikonohack'
version = '1.16-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "placeholderapi-repo"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "DiscordSRV"
        url = 'https://nexus.scarsz.me/content/groups/public/'
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'com.discordsrv:discordsrv:1.28.0'
    compileOnly("net.skinsrestorer:skinsrestorer-api:15.4.1-SNAPSHOT")

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.named('build').configure {
    dependsOn 'shadowJar'
}

shadowJar {
    relocate 'okhttp3', 'com.saikonohack.advancedChat.shaded.okhttp3'
    relocate 'com.google.gson', 'com.saikonohack.advancedChat.shaded.gson'
}